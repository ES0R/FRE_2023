%%%%%%%%%%%%%%%%%%%%%%% task2 %%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%% DTUMaize %%%%%%%%%%%%%%%%%%%%%


%%%%%%%%%%%% constants %%%%%%%%%%%%%
%Startturn: -1 = right, 1 = left

R = -1
L = 1
F = 1000
plan = 0
zero = 0
%turn number of rows-1
turnlim=15

fwdVel = 1.0
preTurnVel = 0.6
slowVel= 0.4
coastVel = 0.4
turnVel = 0.6


turnradiusLong = 0.25
turnradiusShort = 0.35

fwdLaser = 1
bckLaser = 0

fwdLasX = 0.46
bckLasX = -0.17

slowdist= 11

pi=3.141592


%%%%%%%%%%%% variables %%%%%%%%%%%%%
id= 0
turncnt= 1
rowStartX = $odox
rowStartY = $odoy
%turnfinishedX = $odox

Vel = fwdVel
Las = fwdLaser
lasX = fwdLasX

x=0
y=0
th=0
dth = 0
steerTh = 0

xlas=0
ylas=0
thlas=0

holetype=0

%stores previous (x,y,th) values for when exiting row
px=0
py=0
pth=0

turnang = 0
turnrad = 0

exitRowDist = 0
xCurvei = 0
yCurvei = 0
disti = 0
startup = 1

%Filter variables
k1 = 0.8
k2 = 0.2

%%%%%%%%%%%% plugin variables %%%%%%%%%%%%%
wait 1
setoutput "cruizreset" 1
wait 1
%%% odometry set %%%
set "$odox" 0
set "$odoy" 0
set "$odoth" 0

laser "var lobst.minPointsInLine=8"
laser "var lobst.enableSplit=true"
laser "var lobst.minsplitcnt=2"
laser "var lobst.splitdist=0.5"
laser "var rosebot.lineAngleInterval='-105,-75'"
laser "var rosebot.lineRmax=1.0"
laser "var rosebot.holeAngleAllowed=180"

wait 1


%%%%%%%%%%%% holetypes %%%%%%%%%%%%%
% 0 = dead end
% 1 = free space
% 2 = right wall
% 3 = left wall
% 4 = hole
% 5 = ignore hole


%%%%%%%%%%%% start logging %%%%%%%%%%%%%
log "$time" "x" "y" "th" "xx" "yc" "holetype" "Vel" "dth" "steerTh" "turnrad" "turnang"
laser "scanset logopen"
laser "scanset log=1"
laser "odopose log=true"


%%%%%%%%%%% drivePlan array %%%%%%%%%%%%
command = 1
sign = 1
array "drivePlan" 15
drivePlan[0]=5
drivePlan[1]=R
drivePlan[2]=F
drivePlan[3]=F
drivePlan[4]=2
drivePlan[5]=R
drivePlan[6]=3
drivePlan[7]=R
drivePlan[8]=2
drivePlan[9]=R
drivePlan[10]=1
drivePlan[11]=L
drivePlan[12]=F
drivePlan[13]=F

%%%%%%%%%%%%%%%%%% ini %%%%%%%%%%%%%%%%%
label "ini"
	wait 1
	eval $joyoverride
	laser "rosebot"
	backbut = $joyoverride
	setoutput "ledMode" 32
	if (backbut == 1) "ini"
	if (startup == 1) "StartupDrive"
	goto "start"

label "StartupDrive"
	startup = 0
	drive @v0.4 :($drivendist > 0.3)|($cmdtime>2)

%%%%%%%%%%%% start %%%%%%%%%%%%%
label "start"
	setoutput "ledMode" 0

	eval $joyoverride
	backbut = $joyoverride
	if (backbut == 1) "ini"

	laser "rosebot"
	id = id + 1
	stringcat "rosebot device="Las" laserx="lasX" id="id" backward=0"
	laser "$string"
	wait 1 :($l9==id)

	x=$l4
	y=$l5
	th=$l6
	holetype = $l7

	%xlas=$l2
	%ylas=$l3
	%thlas=$l8

	if(holetype == 0) "start"

%%%decide which mode to enter
switch(holetype)
	case 1
		goto "drivePlan"
	case 2
		goto "rowdrive"
	case 3
		goto "rowdrive"
	case 4
		goto "rowdrive"
	case 5
		goto "rowdrive"
	endswitch

	goto "start"


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% rowdrive %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "rowdrive"
	laser "rosebot"
	xo = $odox-rowStartX
	yo = $odoy-rowStartY
	disto = sqrt(xo*xo + yo*yo)

	%%FILTER%%
	sinAv = (sin(th)*k1+sin(pth)*k2)
	cosAv = (cos(th)*k1+cos(pth)*k2)
	steerTh = atan2(sinAv,cosAv)
	%%

	dth = atan2(sin(pth),cos(pth))-atan2(sin(th),cos(th))
	dth = sqrt(dth*dth)

	eval $joyoverride
	backbut = $joyoverride
	if (backbut == 1) "ini"

	if (dth > pi/2) "adjustDTH"

	goto "driveon"


label "adjustDTH"
	dth = dth - 2*pi
	dth = sqrt(dth*dth)

label "driveon"
	setoutput "ledMode" 0
	laser "rosebot"

	Vel = slowVel
	if (disto < 0.5) "driveon3"
	Vel = fwdVel
	if (dth < 15*pi/180) "driveon2"

	xCurvei = $odox
	yCurvei = $odoy
	Vel = slowVel


label "driveon2"
	xi = $odox - xCurvei
	yi = $odoy - yCurvei
	disti = sqrt(xi*xi + yi*yi)

	if (disti < 0.5) "driveon3"

	Vel = fwdVel
	xCurvei=0
	yCurvei=0
	disti=0


label "driveon3"
	%driveon x y th "rad" @v Vel :($drivendist>0.03)
	driveon x y steerTh "rad" @v Vel :($drivendist>0.03)
	px=x
	py=y
	%pth=th
	pth=steerTh
	goto "start"



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%% HEADLAND DRIVING %%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%% drivePlan %%%%%%%%%%%%%%%%%%%%%%%%%%%
label "drivePlan"
	if(drivePlan[plan] == F) "end"

	xe = $odox
	ye = $odoy

	laser "rosebot"
	firstturn = 1
	driveon px py pth "rad" @v preTurnVel :($drivendist>0.3) % exit the row and turn

	exitRowDist = sqrt(($odox-xe)*($odox-xe)+($odoy-ye)*($odoy-ye))

	eval $joyoverride
	backbut = $joyoverride
	if (backbut == 1) "ini"

	odoDist = $ododist

	command = drivePlan[plan] % next row: 1, skip 1 row: 2, skip 2 rows: 3
	sign    = drivePlan[plan+1] % right: -1, left: 1
	plan = plan+2

label "ChooseDirection"
	if(sign == 1) "labell"
	if(sign == -1) "labelr"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% turnr led
label "labelr"
	setoutput "ledMode" 4
	goto "postLed"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%% turnl led
label "labell"
	setoutput "ledMode" 2

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "postLed"
	dturnrad = sin(pth)*exitRowDist

	if (abs(pth) < pi/2) "turnHalf"
	dturnrad = dturnrad*(-1)

	if (pth<0) "adjustUP"
	pth = pth - pi
	goto "turnHalf"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "adjustUP"
	pth = pth + pi

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "turnHalf"
	if(command == 1) "turnOneRow" % make normal turn if no row is to be skipped

	turnang = sign*60 %experiments show 90 degrees causes overshoot
	turnrad = 0.27

	%adjust turning angle and radius during the first turn
	turnang = turnang - pth*180/pi
	turnrad = turnrad - sign*dturnrad/2

	turnr turnrad turnang @v turnVel :($cmdtime > 15)

	if (firstturn == 0) "turnR" %finish headland driving

	% Need stop for laser update
	stop
	fwdDist = (command-1) * 0.75+0.2 %distance the robot must travel to skip rows
	saveOdoy = $odoy
	saveOdox = $odox


%%%%%%%%% driveOffset %%%%%%%%%%%
label "driveOffset"
	setoutput "ledMode" 0
	laser "rosebot"
	sideSelection = sign +1 % right: 0, left: 2 (is bool i ufuncrosebot.cpp l.1728)
	stringcat "rosebot correctPath side="sideSelection" "

	laser "$string"
	yc = $l1
	xx = $l8
	if (xx > 0.0) "skipXX"
	xx = 1.0

label "skipXX"
	eval xx
	if(abs(yc) == 0.0) "skipCorr"  %if yc = 0, then measurement was too small
	yc = 0.7*sign*(-0.65 + abs(yc)) %maintain distance of 70 cm to row edges


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "skipCorr"
	trans $odox $odoy $odoth 5 yc 0
	driveon $res0 $res1 $res2 "rad" @v coastVel :($drivendist>0.02)
	diff = sqrt(($odox-saveOdox)*($odox-saveOdox)+($odoy-saveOdoy)*($odoy-saveOdoy))
	if(diff >= fwdDist) "checkXX"
	goto "driveOffset"

label "checkXX"
	if(xx <= 0.55) "turnIntoRow"

	goto "driveOffset"


%%%%%%%%% turnIntoRow %%%%%%%%%%%
label "turnIntoRow"
	pth = 0
	firstturn=0
	goto "ChooseDirection"


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%% turn when one row %%%%%%%
label "turnOneRow"
	turnang = sign*150
	turnrad = 0.35

	%%%% Deciding the values of the turn radius and turn angle %%%%
	turnang = turnang - pth*180/pi
	turnrad = turnrad - sign*dturnrad/2

	%display error correction
	eval pth*180/pi
	eval sign*dturnrad/2*100

	turnr turnrad turnang @v turnVel :($cmdtime > 20)
	%turnfinishedX = $odox


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%% Turn loop %%%%%%%%%%
label "turnR"
	laser "rosebot"
	id = id + 1
	stringcat "rosebot device="Las" laserx="lasX" id="id" backward=0"
	laser "$string"
	wait 1 :($l9==id)

	%Check for hole or if the robot has turned 180
	if($l7 == 4) "stopR"
	if($ododist < odoDist+1)  "turnR"
	stop
	wait 2
	setoutput "ledMode" 16
	fwd -0.1 @v0.3 :($cmdtime > 10)

	eval $joyoverride
	backbut = $joyoverride
	if (backbut == 1) "ini"

	goto "turnR"


%%%%%%%%%% Hole found %%%%%%%%%%%
label "stopR"
	laser "rosebot"

	%Set values to default
	rowStartX = $odox
	rowStartY = $odoy
	Vel = fwdVel
	goto "start"


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%% end %%%%%%%%%%%%%%%%
label "end"
	setoutput "ledMode" 10 %16
	play "bender.wav"
	fwd 0
	wait 10
	setoutput "ledMode" 0
	stop

	%stop logging
	laser "scanset logclose"
	laser "odopose log=false"
